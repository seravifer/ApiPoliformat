group 'seravifer.apipoliformat'
version '0.4'

buildscript {
    ext.kotlin_version = '1.1.0-rc-91'
    ext.spek_version = '1.1.0-beta3'
    ext.junit_runner_version = '1.0.0-M3'

    repositories {
        mavenCentral()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_runner_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
    platformVersion junit_runner_version
    filters {
        engines {
            include 'spek'
        }
    }
}

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'ch.qos.logback:logback-classic:1.2.1'
    compile 'no.tornado:tornadofx:1.6.1'

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.spek:spek-api:$spek_version"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
}

mainClassName = "seravifer.apipoliformat.App"
task fatJar(type: Jar) {
    archivesBaseName = 'Api-PoliformaT'
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

afterEvaluate {
    def junitPlatformTestTask = tasks.getByName('junitPlatformTest')

    junitPlatformTestTask.systemProperty 'DNI', dni
    junitPlatformTestTask.systemProperty 'PIN', pin
}